#!/usr/bin/perl
# 
# ***** BEGIN LICENSE BLOCK *****
# Zimbra Collaboration Suite Server
# Copyright (C) 2004, 2005, 2006, 2007, 2008, 2009, 2010, 2011, 2012 VMware, Inc.
# 
# The contents of this file are subject to the Zimbra Public License
# Version 1.3 ("License"); you may not use this file except in
# compliance with the License.  You may obtain a copy of the License at
# http://www.zimbra.com/license.
# 
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied.
# ***** END LICENSE BLOCK *****
# 

use strict;

my $id = `id -u -n`;
chomp $id;
if ($id ne "zmail") {die "Run as the zmail user!\n";}

our $zmail_home = "/opt/zmail";

use lib "/opt/zmail/zmailmon/lib";
use Zmail::Util::Common;
use Zmail::Mon::Logger;

use Getopt::Std;
use File::Temp qw/ tempfile /;
use File::Path;

my $zmail_tmp_directory=getLocalConfig("zmail_tmp_directory");

if ( !-d $zmail_tmp_directory ) {
  File::Path::mkpath("$zmail_tmp_directory");
}

my (undef, $zmcontrolLogfile) = tempfile("zmcontrol.out.XXXXX", DIR=>"$zmail_tmp_directory",  OPEN=>0);

my $timeout = 180;
$SIG{ALRM} = sub { print "Timeout after $timeout seconds\n"; exit(1) };
my $now = time();
my $cache_file = "${zmail_home}/log/.zmcontrol.cache";
my $localHostName = getHostName();
my $ldapHere = isLdapLocal();
my $services;
my %devicesChecked;
my %serviceStatusList;
my %startorder = ( 
	"ldap" 		=> 0,
	"zmconfigd"	=> 5,
	"logger" 	=> 10,
	"convertd"	=> 20,
	"mailbox" 	=> 30,
	"memcached"	=> 40,
	"proxy"		=> 50,
	"antispam" 	=> 60,
	"antivirus"	=> 70,
	"opendkim"	=> 75,
	"archiving"	=> 80,
	"cbpolicyd"	=> 85,
	"snmp" 		=> 90,
	"spell" 	=> 100,
	"mta" 		=> 110,
	"stats"		=> 120,
	"vmware-ha" => 150,  # this should remain last
);

my %stoporder = ( 
	"ldap" 		=> 0,
	"logger" 	=> 10,
	"convertd"	=> 20,
	"mailbox" 	=> 30,
	"memcached"	=> 40,
	"proxy"		=> 50,
	"antispam" 	=> 60,
	"antivirus"	=> 70,
	"opendkim"	=> 75,
	"archiving"	=> 80,
	"cbpolicyd"	=> 85,
	"snmp" 		=> 90,
	"spell" 	=> 100,
	"mta" 		=> 110,
	"stats"		=> 120,
	"zmconfigd"	=> 130,
	"vmware-ha"	=> 140,
);

my %allservices = ( 
	"antivirus" => "/opt/zmail/bin/zmantivirusctl", 
	"antispam" => "/opt/zmail/bin/zmantispamctl", 
	"archiving" => "/opt/zmail/bin/zmarchivectl",
	"opendkim" => "/opt/zmail/bin/zmopendkimctl",
	"convertd" => "/opt/zmail/bin/zmconvertctl",
	"mta" => "/opt/zmail/bin/zmmtactl", 
	"mailbox" => "/opt/zmail/bin/zmstorectl", 
	"logger" => "/opt/zmail/bin/zmloggerctl", 
	"snmp" => "/opt/zmail/bin/zmswatchctl", 
	"ldap" => "/opt/zmail/bin/ldap",
	"spell" => "/opt/zmail/bin/zmspellctl",
	"memcached" => "/opt/zmail/bin/zmmemcachedctl",
	"proxy" => "/opt/zmail/bin/zmproxyctl",
	"stats" => "/opt/zmail/bin/zmstatctl",
	"zmconfigd" => "/opt/zmail/bin/zmconfigdctl",
	"cbpolicyd" => "/opt/zmail/bin/zmcbpolicydctl",
	"vmware-ha" => "/opt/zmail/bin/zmhactl",
);

my %rewrites = ( 
	"antivirus" => "antivirus amavis",
	"antispam" => "antispam amavis",
	"archiving" => "archiving amavis",
	"opendkim" => "opendkim",
	"mta" => "amavis antispam antivirus opendkim mta sasl",
	"mailbox" => "webxml mailbox",
	"proxy" => "proxy",
);

my %GlobalOpts = ();

my %DESC = (
	"start" => "Start services",
	"startup" => "Start services",
	"stop" => "Stop services",
	"shutdown" => "Stop services",
	"restart" => "Restart services",
	#"maintenance" => "Toggle maintenance mode",
	"status" => "Display service status",
);

my %COMMANDS = (
	"start" => \&doStartup,
	"startup" => \&doStartup,
	"stop" => \&doShutdown,
	"shutdown" => \&doShutdown,
	"restart" => \&doRestart,
	#"maintenance" => \&setMaintenanceMode,
	"status" => \&doStatus,
);

my %REMOTECOMMANDS = (
	"start" => "startup",
	"startup" => "startup",
	"stop" => "shutdown",
	"shutdown" => "shutdown",
	"restart" => "restart",
	#"maintenance" => "maintenance",
	"status" => "status",
);


$| = 1;

unless ( getopts( 'vhH:', \%GlobalOpts ) ) { usage(); }

if ( ! $GlobalOpts{H} ) { $GlobalOpts{H} = $localHostName; chomp $GlobalOpts{H}; }

if ( $GlobalOpts{h} ) { usage(); }
if ( $GlobalOpts{v} ) { displayVersion(); exit 0;}

# Commands: start, stop, restart and status
my $command = $ARGV[0];

unless (defined ($COMMANDS{$command})) {usage();}

if ($GlobalOpts{H} ne $localHostName) {
	exit (runRemoteCommand ($command));
}

exit (&{$COMMANDS{$command}}($ARGV[1]));

#
# Functions
#
sub runRemoteCommand {
	my $cmd = shift;
	my $cstr = "HOST:$GlobalOpts{H} $REMOTECOMMANDS{$cmd}";

	open (REMOTE, "echo $cstr | /opt/zmail/libexec/zmrc $GlobalOpts{H} |") or 
		die "Can't contact $GlobalOpts{H}";

	while (<REMOTE>) {
		if (/^STARTCMD: (\S+) .*/) {
			#print "Host $1 starting\n";
		} elsif (/^ENDCMD: (\S+) .*/) {
			print "Host $1 complete\n";
			exit; # Since the pipe doesn't always like to close...
		} else {
			print "$_";
		}
	}
	close REMOTE;
}

sub doStatus {
	$services = getEnabledServices();
  getServiceStatusList();
  alarm($timeout);
  my (undef, $statusfile) = tempfile("zmcontrol.status.XXXXX", DIR=>"$zmail_tmp_directory",  OPEN=>0);
  my (undef, $errfile) = tempfile("zmcontrol.error.XXXXX", DIR=>"$zmail_tmp_directory",  OPEN=>0);
	if (scalar(keys %$services) == 0) {
		print "Cannot determine services - exiting\n";
		return 1;
	}
	my $status = 0;
	print "Host $localHostName\n";
	foreach (sort keys %{$services}) {
		my $rc = 0xffff & system ("$allservices{$_} status > $statusfile 2> $errfile");
		$rc = $rc >> 8;
		if ($rc) {
      # this is an any ugly hack for 11266
      $status = 1 if ($serviceStatusList{$_});
        
		}
		my $stat = sprintf "\t%-20s %10s\n",$_,($rc)?"Stopped":"Running";
		print "$stat";
		if ($rc) {
			open (ST, "$statusfile") or next;
			foreach my $s (<ST>) {
				print "\t\t$s";
			}
			close ST;
		}
	}
	unlink($statusfile);
	unlink($errfile);
  alarm(0);
	return $status;
}

sub startLdap {
	print "\tStarting ldap...";
	my $rc = 0xffff & system("/opt/zmail/bin/ldap start > $zmcontrolLogfile 2>&1");
	$rc = $rc >> 8;
	print "Done.\n";
	return $rc;
}

sub doRewrite {
	my $rew = "";
	foreach (@_) {
		$rew .= " $rewrites{$_}";
	}
	Zmail::Mon::Logger::Log ("info", "Rewriting configs $rew");
	my $rc = 0xffff & system("/opt/zmail/libexec/configrewrite $rew > $zmcontrolLogfile 2>&1");
	$rc = $rc >> 8;
	return $rc;
}

sub doRestart {
  &doShutdown;
  &doStartup;
}

sub doStartup {
	Zmail::Mon::Logger::Log ("info", "Starting services initiated by zmcontrol");
	print "Host $localHostName\n";
	my $rc = 0;
	my $rrc = 0;
	if ($ldapHere) {
		my $ldapStopped = 0xffff & system("/opt/zmail/bin/ldap status > /dev/null 2>&1");
		if ($ldapStopped) {
			$rrc = startLdap();
		}
	}
	if ($rrc) {
		$rc = 1;
		my $out = `cat $zmcontrolLogfile`;
		print "Failed.\n";
		print "$out\n\n";
		exit (1);
	}
	unlink($zmcontrolLogfile);
	$services = getEnabledServices();
	if (scalar(keys %$services) == 0) {
		return 1;
	}
	if (defined ($$services{"ldap"}) ) {
		my $ldapStopped = 0xffff & system("/opt/zmail/bin/ldap status > /dev/null 2>&1");
		if ($ldapStopped) {
			$rrc = startLdap();
			sleep 3;
		}
	}
	if ($rrc) {
		$rc = 1;
		my $out = `cat $zmcontrolLogfile`;
		print "Failed.\n";
		print "$out\n\n";
		exit (1);
	}

	checkAvailableSpace();
	foreach (sort {$startorder{$a} <=> $startorder{$b}} keys %{$services}) {
		if ($_ eq "ldap") {next;}
		checkAvailableServiceSpace($_);
		Zmail::Mon::Logger::Log ("info", "Starting $_ via zmcontrol");
		print "\tStarting $_...";
		unless (-x "$allservices{$_}") {
			print "skipped.\n\t\t$allservices{$_} missing or not executable.\n";
			next;
		}
		$rrc = 0xffff & system ("$allservices{$_} start norewrite > $zmcontrolLogfile 2>&1");
		$rrc = $rrc >> 8;
		if ($rrc) {
			$rc = 1;
			my $out = `cat $zmcontrolLogfile`;
			print "Failed.\n";
			print "$out\n\n";
		} else {
			print "Done.\n";
		}
		unlink($zmcontrolLogfile);
	}
	return $rc;
}

sub doShutdown {
	Zmail::Mon::Logger::Log ("info", "Stopping services initiated by zmcontrol");
	print "Host $localHostName\n";
	my $rc = 0;
	my $rrc = 0;
	foreach (sort {$stoporder{$b} <=> $stoporder{$a}} keys %allservices) {
		Zmail::Mon::Logger::Log ("info", "Stopping $_ via zmcontrol");
		if ($_ eq "ldap" && !(-d "/opt/zmail/openldap/sbin") ) { next; }
		if ($_ eq "mta" && !(-d "/opt/zmail/postfix/libexec") ) { next; }
		if ($_ eq "snmp" && !(-f "/opt/zmail/bin/zmswatchctl") ) { next; }
		if ($_ eq "mailbox" && !(-d "/opt/zmail/db/data") ) { next; }
		if ($_ eq "convertd" && !(-l "/opt/zmail/keyview") ) { next; }
		print "\tStopping $_...";
    unless (-x "$allservices{$_}") {
      print "skipped.\n\t\t$allservices{$_} missing or not executable.\n";
      next;
    }
		$rrc = 0xffff & system ("$allservices{$_} stop > $zmcontrolLogfile 2>&1");
		$rrc = $rrc >> 8;
		if ($rrc) {
			$rc = 1;
			my $out = `cat $zmcontrolLogfile`;
			print "Failed.\n";
			print "$out\n\n";
		} else {
			print "Done.\n";
		}
		unlink($zmcontrolLogfile);
	}
	return $rc;
}

sub setMaintenanceMode {
	my $mode = shift;
}

sub getServiceStatusList {
  my @services = split(/\s+/, getLocalConfig("zmcontrol_service_status_list"));
  @services = grep(!/stats|snmp|logger|spell/, keys %allservices) if (scalar @services < 1);
  if (scalar @services > 1) {
    foreach my $service (@services) {
      $serviceStatusList{$service} = 1
        if (defined($allservices{$service}));
    }
  }
}
    

sub getLocalConfig {
	my $key = shift;
	if (defined ($ENV{zmsetvars})) {
		return $ENV{$key};
	}
	open CONF, "/opt/zmail/bin/zmlocalconfig -x -s -q -m shell |" or
		die "Can't open zmlocalconfig: $!";
	my @conf = <CONF>;
	close CONF;

	chomp @conf;

	foreach (@conf) {
		my ($key, $val) = split '=', $_, 2;
		$val =~ s/;$//;
		$val =~ s/'$//;
		$val =~ s/^'//;
		$ENV{$key} = $val;
	}
	$ENV{zmsetvars} = 'true';
	return $ENV{$key};
}

sub getCachedServices {

  my %s = ();
  $s{"zmconfigd"} = "zmconfigd";
	if (-f $cache_file && -M $cache_file <= 1) {
    open(CACHE, "<$cache_file");
    my @lines = <CACHE>;
    close CACHE;
    foreach (@lines) {
      chomp;
      $s{$_} = $_;
    }
  } else {
    print "Unable to determine enabled services. Cache is out of date or doesn't exist.\n"; 
    exit 1;
  }
  warn "Enabled services read from cache. Service list may be inaccurate.\n"
    if (scalar keys %s > 0 );
  return \%s;
}

sub getEnabledServices {
	unless (open (ZMPROV, "/opt/zmail/bin/zmprov -l gs $localHostName 2>/dev/null |")) {
    warn "Unable to determine enabled services from ldap.\n";
    return getCachedServices();
  }
  my @lines = <ZMPROV>;
  unless (close ZMPROV) {
    warn "Unable to determine enabled services from ldap.\n";
    return getCachedServices();
  }

  my %s = ();
  $s{"zmconfigd"} = "zmconfigd";
  foreach (@lines) {
    if (m/^zmailServiceEnabled: (.*)/) {
      $s{$1} = $1;
    }
  }
  if (scalar keys %s > 0) {
    open (CACHE, ">$cache_file");
    foreach my $service (keys %s) {
      print CACHE "$service\n";
    }
    close(CACHE);
  }
  return \%s;
}

sub isLdapLocal {
  return((index(getLocalConfig("ldap_url"), "/".getLocalConfig("zmail_server_hostname")) != -1) ? 1 : 0);
}

sub getHostName {
	return (getLocalConfig("zmail_server_hostname"));
}

sub displayVersion {
	my $platform = `/opt/zmail/libexec/get_plat_tag.sh`;
	chomp $platform;
	my $string = "";
	if ($platform =~ /MACOSX/) {
    my $release;
    if (-x "/usr/sbin/pkgutil") {
      $release=`/usr/sbin/pkgutil --pkg-info org.zmail.zcs.core  2> /dev/null | grep ^version | awk '{print \$NF}'`;
    } else {
		  $release=`grep -C1 CFBundleGetInfoString /Library/Receipts/zmail-core.pkg/Contents/Info.plist | tail -1 | sed -e 's/\<[^>]*\>//g' -e 's/^ *//g' | awk -F, '{print $1}'`;
    }
    chomp($release);
		$string = "Release $release $platform";
	} elsif ($platform =~ /DEBIAN/ || $platform =~ /UBUNTU/) {
		my $release = `dpkg -s zmail-core | egrep '^Version:' | sed -e 's/Version: //'`;
		chomp $release;
    if ( -x "/opt/vmware/bin/vamicli") {
      my $appliance_version=`/opt/vmware/bin/vamicli version --appliance | awk '{if ((\$1 ~ /Version/) && (\$2 ~ /-/)) { print \$3} }' 2> /dev/null`;
      chomp($appliance_version);
      $string = "ZCA Release $appliance_version\n";
      $string .= "ZCS Build $release";
    } else {
		  $string = "Release $release $platform";
    }
	} elsif ($platform =~ /RPL/) {
		my $release = `conary q zmail-core | cut -d= -f2`;
		$string = "Release $release $platform";

	} else {
		my $release = `rpm -q --queryformat "%{version}_%{release}" zmail-core`;
		my $inst = localtime (`rpm -q --queryformat "%{installtime}" zmail-core`);
		$string = "Release $release $platform";
	}

  $string .=  ((-f "/opt/zmail/bin/zmbackupquery") ? " NETWORK" : " FOSS"); 
  $string .= " edition";

  my $patch = "";
  if (open(HIST_FILE, $zmail_home . "/.install_history")) {
    my $hline;
    while ($hline = <HIST_FILE>) {
      my $entry;
  
      chomp($hline);
      (undef, $entry) = split(': ', $hline);
      if ($entry =~ /CONFIGURED patch/) {
        $patch = substr($entry, 16)
      }
      if ($entry =~ /INSTALL SESSION START/) {
        $patch = "";
      }
    }
    close(HIST_FILE);
  }
  if ($patch ne "") {
    $string .= ", Patch $patch";
  }

	print "$string.\n";
}

sub checkAvailableSpace {
  my ($service) = @_;

  my @dirs = ("/opt/zmail");
  foreach my $dir (@dirs) {
    if (-e "$dir") {
      print "\tWARNING: Disk space below threshold for $dir.\n"
        unless hasAvailableSpace($dir);
    }
  }
}
sub checkAvailableServiceSpace {
  my ($service) = @_;

  my %serviceDirs = ( 
    mailbox => ["/opt/zmail/store", "/opt/zmail/db", "/opt/zmail/index", "/opt/zmail/redolog"],
    logger => ["/opt/zmail/logger/db"],
    mta => ["/opt/zmail/data/postfix/spool"]
  );  
  my @dirs = ();
  @dirs = (@dirs, @{$serviceDirs{$service}})
    if (defined $serviceDirs{$service});
  foreach my $dir (@dirs) {
    if (-e "$dir") {
      print "\tWARNING: Disk space below threshold for $dir.\n"
        unless hasAvailableSpace($dir);
    }
  }
}

sub hasAvailableSpace() {
  my ($dir,$freeMbytes) = @_;
    return undef unless (-e "$dir");
  $freeMbytes = getLocalConfig("zmail_disk_threshold") || 100
    unless $freeMbytes;
  my $zmail_home = "/opt/zmail";
  my $platform=`${zmail_home}/libexec/get_plat_tag.sh`;
  chomp $platform;

  my $DFCMD = ($platform =~ /MACOSX/ ? "df -ml " : "df -mlP ");

  open DF, "$DFCMD $dir | tail -1 |" or die "Can't open $DFCMD: $!";
  my @df = <DF>;
  close DF;
  my ($device, $total, undef, $avail) = split(/\s+/, $df[0]);
  return 1 if (defined($devicesChecked{$device})); 
  $devicesChecked{$device} = $avail;
  if ($avail < $freeMbytes) { 
	  Zmail::Mon::Logger::Log ("info", "Availble disk space on $dir is below threshold of $freeMbytes.  $avail Mbytes available.");
  }

  return ($avail > $freeMbytes) ? 1 : undef;
}

sub usage {
	displayVersion();
	print "$0 [-v -h -H <host>] command [args]\n";
	print "\n";
	print "\t-v:	display version\n";
	print "\t-h:	print usage statement\n";
	print "\t-H:	Host name (localhost)\n";
	print "\n";
	print "\tCommand in:\n";
	foreach ( sort keys %COMMANDS ) {
		print "\t\t" . sprintf( "%-20s%30s", $_, $DESC{$_} ) ."\n";
	}

	print "\n";
	exit 1;
}
