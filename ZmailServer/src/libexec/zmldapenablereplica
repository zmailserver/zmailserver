#!/usr/bin/perl
# 
# ***** BEGIN LICENSE BLOCK *****
# Zimbra Collaboration Suite Server
# Copyright (C) 2005, 2006, 2007, 2008, 2009, 2010, 2011, 2012 VMware, Inc.
# 
# The contents of this file are subject to the Zimbra Public License
# Version 1.3 ("License"); you may not use this file except in
# compliance with the License.  You may obtain a copy of the License at
# http://www.zimbra.com/license.
# 
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied.
# ***** END LICENSE BLOCK *****
# 

use strict;
use lib '/opt/zmail/zmailmon/lib';
use Net::LDAP;
use XML::Simple;
use Getopt::Long;
use File::Copy qw/ cp /;
use File::Path;

if ( ! -d "/opt/zmail/openldap/etc" ) {
  print "ERROR: openldap does not appear to be installed - exiting\n";
  exit(1);
}

my $id = getpwuid($<);
chomp $id;
if ($id ne "zmail") {
    print STDERR "Error: must be run as zmail user\n";
    exit (1);
}

my ($help);

my $opts_good = GetOptions(
  'h|help' => \$help,
);

if (!$opts_good) {
  print STDERR "\n";
  usage();
}
if ($help) {
  usage(0);
}

my $localxml = XMLin("/opt/zmail/conf/localconfig.xml");

my $ldap_root_password = $localxml->{key}->{ldap_root_password}->{value};
chomp($ldap_root_password);
my $zmail_home = $localxml->{key}->{zmail_home}->{value};
my $ldap_replication_password = $localxml->{key}->{ldap_replication_password}->{value};
my $ldap_starttls_supported = $localxml->{key}->{ldap_starttls_supported}->{value};
my $zmail_require_interprocess_security = $localxml->{key}->{zmail_require_interprocess_security}->{value};
my $zmail_server_hostname = $localxml->{key}->{zmail_server_hostname}->{value};
my $ldap_master_url = $localxml->{key}->{ldap_master_url}->{value};
my $ldap_port = $localxml->{key}->{ldap_port}->{value};
my $zmail_ldap_password = $localxml->{key}->{zmail_ldap_password}->{value};
my $zmail_ldap_userdn = $localxml->{key}->{zmail_ldap_userdn}->{value};

if ($zmail_home eq "") {
   $zmail_home = "/opt/zmail";
}

my $zmprov="${zmail_home}/bin/zmprov";

my ($lmr, $junk) = split ' ', $ldap_master_url, 2;
my $ldap_master_host=$lmr;
$ldap_master_host =~ s/ldaps*:\/\///;
$ldap_master_host =~ s/:.*//;

my $proto;

if ( $ldap_port == 636 ) {
	$proto = "ldaps";
} else { 
	$proto = "ldap";
}

sub verifyLdap {
  # Ensure we can bind to the master server before doing anything else

  print "Verifying ldap on $lmr...";
  my $ldap = Net::LDAP->new("$lmr")  or  die "$@";

  # startTLS Operation
  my $mesg;
  if ($lmr !~ /^ldaps/i) {
    if ($ldap_starttls_supported) {
      $mesg = $ldap->start_tls(
        verify => 'none',
        capath => "$zmail_home/conf/ca",
      ) or die "start_tls: $@";
      $mesg->code && die "TLS: " . $mesg->error . "\n";
    }
  }
  $mesg = $ldap->bind("$zmail_ldap_userdn", password=>"$zmail_ldap_password");
  $mesg->code && die "ERROR: Unable to verify ldap connection on $lmr - exiting\n";
  $ldap->unbind;
  print "succeeded\n";
}

sub verifyAccesslog {
  # Verify that accesslog DB doesn't already exist.

  my $ldap = Net::LDAP->new('ldapi://%2fopt%2fzmail%2fopenldap%2fvar%2frun%2fldapi/') or die "$@";
  my $mesg;
  $mesg = $ldap->bind("cn=config", password=>"$ldap_root_password");

  my $base="cn=accesslog";
  $mesg = $ldap ->search(
                    base=>"$base",
                    filter=>"(objectClass=*)",
                    scope=>"base",
                    attrs => ['1.1'],
                );
  my $size = $mesg->count;
  $ldap->unbind;
  if ($size > 0) {
    print "Accesslog is already enabled. Exiting...\n";
    exit(1);
  }
}

sub verifySyncProv {
  my $ldap = Net::LDAP->new('ldapi://%2fopt%2fzmail%2fopenldap%2fvar%2frun%2fldapi/') or die "$@";
  my $mesg;

  $mesg = $ldap->bind("cn=config", password=>"$ldap_root_password");
  my $base = "olcDatabase={2}mdb,cn=config";

  $mesg = $ldap ->search(
                    base=>"$base",
                    filter=>"(olcOverlay=syncprov)",
                    scope=>"sub",
                    attrs => ['1.1'],
                );
  my $size = $mesg->count;
  $ldap->unbind;
  if ($size > 0) {
    print "Error: Syncprov already enabled. Exiting...\n";
    exit 1;
  }
}

sub enableSyncProv {
  print "Enabling sync provider on master...";
  File::Path::mkpath("${zmail_home}/data/ldap/accesslog/db");

  my $ldap = Net::LDAP->new('ldapi://%2fopt%2fzmail%2fopenldap%2fvar%2frun%2fldapi/') or die "$@";
  my $mesg = $ldap->bind("cn=config", password=>"$ldap_root_password");
  $mesg->code && die "Bind: ". $mesg->error . "\n";

  my $bdn="olcDatabase={2}mdb,cn=config";
  $mesg=$ldap->add(
    $bdn,
    attr => [
      objectClass=>["olcDatabaseConfig","olcMdbConfig"],
      olcDatabase=>"{2}mdb",
      olcDbDirectory=>"${zmail_home}/data/ldap/accesslog/db",
      olcSuffix=>"cn=accesslog",
      olcAccess=>'{0}to dn.subtree="cn=accesslog"  by dn.exact="uid=zmail,cn=admins,cn=zmail" read  by dn.exact="cn=config" read  by dn.exact="uid=zmreplica,cn=admins,cn=zmail" read',
      olcLastMod=>"TRUE",
      olcMaxDerefDepth=>"15",
      olcReadOnly=>"FALSE",
      olcRootDN=>"cn=config",
      olcSizeLimit=>"unlimited",
      olcTimeLimit=>"unlimited",
      olcMonitoring=>"TRUE",
      olcDbCheckpoint=>"0 0",
      olcDbEnvFlags=>["writemap","nometasync"],
      olcDbNoSync=>"TRUE",
      olcDbIndex=>["entryCSN eq", "objectClass eq", "reqEnd eq", "reqResult eq", "reqStart eq"],
      olcDbMode=>"0600",
      olcDbSearchStack=>"16",
      olcDbMaxsize=>"85899345920",
    ],
  );
  $mesg->code && die "Add Failed: ". $mesg->error . "\n";
  $mesg=$ldap->add(
    'olcOverlay=syncprov,olcDatabase={2}mdb,cn=config',
    attr => [
      objectClass=>["olcOverlayConfig", "olcSyncProvConfig"],
      olcOverlay=>"syncprov",
      olcSpNoPresent=>"TRUE",
      olcSpReloadHint=>"TRUE",
    ],
  );
  $mesg->code && die "Add Failed: ". $mesg->error . "\n";
  $mesg=>$ldap->add(
    'olcOverlay={0}syncprov,olcDatabase={3}mdb,cn=config',
    attr => [
      objectClass=>['olcOverlayConfig','olcSyncProvConfig'],
      olcOverlay=>'{0}syncprov',
      olcSpCheckpoint=>'20 10',
    ],
  );
  $mesg->code && die "Add Failed: ". $mesg->error . "\n";
  $mesg=>$ldap->add(
    'olcOverlay={1}accesslog,olcDatabase={3}mdb,cn=config',
    attr => [
      objectClass=>['olcOverlayConfig','olcAccessLogConfig'],
      olcOverlay=>'{1}accesslog',
      olcAccessLogDB=>'cn=accesslog',
      olcAccessLogOps=>'writes',
      olcAccessLogSuccess=>'TRUE',
      olcAccessLogPurge=>'01+00:00  00+04:00',
    ],
  );
  $mesg->code && die "Add Failed: ". $mesg->error . "\n";
  $ldap->unbind;
  print "succeeded\n";
}

sub createLdapConfig {
  # Verify syncprov isn't already enabled on the db
  print "Enabling sync replication on replica...";

  my $ldaps=0;
  if ($lmr =~ /^ldaps/) {
    $ldaps=1;
  }

  my $ldap = Net::LDAP->new('ldapi://%2fopt%2fzmail%2fopenldap%2fvar%2frun%2fldapi/') or die "$@";
  my $mesg = $ldap->bind("cn=config", password=>"$ldap_root_password");
  $mesg->code && die "Bind: ". $mesg->error . "\n";

  my $bdn = "olcDatabase={2}mdb,cn=config";
  if ($ldaps) {
    $mesg = $ldap->modify(
      $bdn,
      add=>[
        olcSyncrepl=>"rid=100 provider=$lmr bindmethod=simple timeout=0 network-timeout=0 binddn=uid=zmreplica,cn=admins,cn=zmail credentials=$ldap_replication_password filter=\"(objectclass=*)\" searchbase=\"\" logfilter=\"(&(objectClass=auditWriteObject)(reqResult=0))\" logbase=cn=accesslog scope=sub schemachecking=off type=refreshAndPersist retry=\"60 +\" syncdata=accesslog tls_cacertdir=/opt/zmail/conf/ca keepalive=240:10:30",
        olcUpdateRef=>"$lmr",
      ],
    );
  } elsif ($ldap_starttls_supported && $zmail_require_interprocess_security) {
    $mesg = $ldap->modify(
      $bdn,
      add=>[
        olcSyncrepl=>"rid=100 provider=$lmr bindmethod=simple timeout=0 network-timeout=0 binddn=uid=zmreplica,cn=admins,cn=zmail credentials=$ldap_replication_password starttls=critical filter=\"(objectclass=*)\" searchbase=\"\" logfilter=\"(&(objectClass=auditWriteObject)(reqResult=0))\" logbase=cn=accesslog scope=sub schemachecking=off type=refreshAndPersist retry=\"60 +\" syncdata=accesslog tls_cacertdir=/opt/zmail/conf/ca keepalive=240:10:30",
        olcUpdateRef=>"$lmr",
      ],
    );
  } else {
    $mesg = $ldap->modify(
      $bdn,
      add=>[
        olcSyncrepl=>"rid=100 provider=$lmr bindmethod=simple timeout=0 network-timeout=0 binddn=uid=zmreplica,cn=admins,cn=zmail credentials=$ldap_replication_password filter=\"(objectclass=*)\" searchbase=\"\" logfilter=\"(&(objectClass=auditWriteObject)(reqResult=0))\" logbase=cn=accesslog scope=sub schemachecking=off type=refreshAndPersist retry=\"60 +\" syncdata=accesslog tls_cacertdir=/opt/zmail/conf/ca keepalive=240:10:30",
        olcUpdateRef=>"$lmr",
      ],
    );
  }
  if ($mesg->code) {
    print "FAILED\n";
    print "ERROR: Unable to add syncrepl configuration to $zmail_server_hostname - exiting\n";
    print "ERROR: ".$mesg->error."\n";
    $ldap->unbind;
    &resetLdapUrl;
    exit 1;
  }
  # ADD SYNCPROV overlay to DB
  $bdn="olcOverlay={0}syncprov,olcDatabase={2}mdb,cn=config";
  $mesg = $ldap->add(
    $bdn,
    attr => [
      objectClass=>['olcOverlayConfig','olcSyncProvConfig'],
      olcOverlay=>"{0}syncprov",
    ],
  );
  $ldap->unbind;
  if ($mesg->code) {
    print "FAILED";
    print "ERROR: Unable to add syncprov overlay to ${zmail_server_hostname} - exiting\n";
    print "ERROR: " . $mesg->error . "\n";
    &resetLdapUrl;
    exit 1;
  } else {
    print "succeeded\n";
  }
}

sub enableLdapService {
  print "Enabling LDAP service on ${zmail_server_hostname}...";

  open(ZMPROV, "${zmprov} -m -l gs ${zmail_server_hostname} zmailServiceEnabled=ldap 2> /dev/null|");
  my @CONFIG=<ZMPROV>;
  close(ZMPROV);
  my $found=0;
  foreach my $line (@CONFIG) {
    if ($line =~ /^zmailServiceEnabled: ldap$/) {
      $found=1;
    }
  }

  if (!$found) {
    system("${zmail_home}/bin/zmprov -m -l ms ${zmail_server_hostname} +zmailServiceEnabled ldap");
  }

  print "succeeded\n"; 
}

sub updateLdapHost {
	print "Setting ldap_url on ${zmail_server_hostname}...";
	`${zmail_home}/bin/zmlocalconfig -f -e ldap_url="${proto}://${zmail_server_hostname}:${ldap_port} ${lmr}"`;
	print "done\n";
}

sub resetLdapUrl {
	print "Resetting ldap_url on ${zmail_server_hostname}...";
	`${zmail_home}/bin/zmlocalconfig -f -e ldap_url="${lmr}"`;
	print "done\n";
}

sub startLdap {
  print "Starting LDAP on ${zmail_server_hostname}...";
  my $cmd = "$zmail_home/bin/ldap status";
  my $rc = system($cmd);
  $rc >>= 8;
  if ($rc != 0) {
    $cmd = "$zmail_home/bin/ldap start";
    $rc = system($cmd);
    $rc >>= 8;
    if ($rc == 1) {
      print "Error: Unable to start ldap, exiting.\n";
      &resetLdapUrl;
      exit 1;
    }
  }
  print "done\n";
}

sub usage {
  my ($msg) = (@_);
  $msg && print STDERR "\nERROR: $msg\n";

  print STDERR <<USAGE;
  Usage: zmldapenablereplica

  Use zmldapenablereplica to set up this server as an ldap replica
  or to enable the replication database on the master

  zmldapenablereplica must have been run on the master prior to being
  run on any replicas.  It is only necessary to run zmldapenablereplica
  once on the master.

USAGE
  exit(1);
}

if ( lc($ldap_master_host) eq lc($zmail_server_hostname) ) {
  &verifyAccesslog;
  &verifySyncProv;
  &enableSyncProv;
  exit 0;
}

&verifyLdap;
&updateLdapHost;
&startLdap;
&verifyAccesslog;
&verifySyncProv;
&createLdapConfig;
&enableLdapService;
